buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.4.0'
        classpath 'com.moowork.gradle:gradle-node-plugin:0.8'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'coveralls'
apply plugin: 'application'
apply plugin: 'com.moowork.node'

defaultTasks 'build'

version = '0.3-SNAPSHOT'

def libs = [
        gson              : 'com.google.code.gson:gson:2.2.4',
        molecule          : 'com.vtence.molecule:molecule:0.4',
        simple            : 'org.simpleframework:simple:5.1.6',
        jmustache         : 'com.samskivert:jmustache:1.9',
        hamcrest_all      : 'org.hamcrest:hamcrest-all:1.3',
        junit             : 'junit:junit:4.11@jar',
        jmock             : 'org.jmock:jmock:2.6.0@jar',
        jmock_junit4      : 'org.jmock:jmock-junit4:2.6.0@jar',
        htmlunit          : 'net.sourceforge.htmlunit:htmlunit:2.14',
        molecule_test     : 'com.vtence.molecule:molecule:0.4:tests',
        hamcrest_dom      : ['com.google.guava:guava:15.0',
                             'org.antlr:antlr-runtime:3.0.1',
                             'com.3levers.cssselectors:cssselectors:1.0@jar',
                             'org.testinfected.hamcrest-matchers:dom-matchers:1.5@jar'],
        selenium_api      : 'org.seleniumhq.selenium:selenium-api:2.44.0',
        selenium_remote   : 'org.seleniumhq.selenium:selenium-remote-driver:2.44.0',
        selenium_firefox  : 'org.seleniumhq.selenium:selenium-firefox-driver:2.44.0',
        selenium_phantom  : 'com.github.detro:phantomjsdriver:1.2.0',
        windowlicker_core : 'com.googlecode.windowlicker:windowlicker-core:r268@jar',
        windowlicker_web  : 'com.googlecode.windowlicker:windowlicker-web:r268@jar',
]

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile libs.gson
    compile libs.molecule
    compile libs.simple
    compile libs.jmustache

    testCompile libs.hamcrest_all
    testCompile libs.junit
    testCompile libs.jmock
    testCompile libs.jmock_junit4
    testCompile libs.htmlunit
    testCompile libs.molecule_test
    testCompile libs.hamcrest_dom
    testCompile libs.selenium_api
    testCompile libs.selenium_firefox
    testCompile(libs.selenium_phantom) {
        exclude module: 'selenium-java'
        exclude module: 'selenium-server'
    }
    testCompile libs.windowlicker_core
    testCompile libs.windowlicker_web
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task coverage(dependsOn: ['test', 'jacocoTestReport'])

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

ext {
    karmaDependencies = ['karma', 'karma-mocha', 'karma-chai', 'karma-effroi', 'karma-phantomjs-launcher']
    karmaExec = file('node_modules/karma/bin/karma')
    karmaConfig = 'src/test/resources/karma.conf.js'
}

task karmaSetup(type: NpmTask) {
    outputs.dir file('node_modules')
    args = ['install', '--loglevel', 'error'] + karmaDependencies
}

task karmaRun(type: NodeTask, dependsOn: 'karmaSetup') {
    script = karmaExec
    args = ['start', karmaConfig, '--single-run']
}

task karmaWatch(type: NodeTask, dependsOn: 'karmaSetup') {
    script = karmaExec
    args = ['start', karmaConfig]
}

test.dependsOn karmaRun

mainClassName = 'com.vtence.jyose.JYose'
applicationName = 'jyose'

if (!project.hasProperty('port')) {
    ext.port = 8080
}

run {
    args port
    args file('src/main/webapp')
}

task stage(dependsOn: ['clean', 'installApp'])

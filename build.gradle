buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.4.0'
        classpath 'com.moowork.gradle:gradle-node-plugin:0.8'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'coveralls'
apply plugin: 'application'
apply plugin: 'com.moowork.node'

defaultTasks 'build'

version = '0.3-SNAPSHOT'

def libs = [
        gson              : 'com.google.code.gson:gson:2.2.4',
        molecule          : 'com.vtence.molecule:molecule:0.10',
        jmustache         : 'com.samskivert:jmustache:1.11',
        simple            : ['org.simpleframework:simple-common:6.0.1',
                             'org.simpleframework:simple-transport:6.0.1',
                             'org.simpleframework:simple-http:6.0.1'],
        junit             : 'junit:junit:4.12@jar',
        jmock             : ['org.jmock:jmock:2.8.1@jar',
                             'org.jmock:jmock-junit4:2.8.1@jar'],
        hamcrest          : ['org.hamcrest:java-hamcrest:2.0.0.0',
                             'org.hamcrest:hamcrest-junit:2.0.0.0'],
        hamcrest_dom      : ['se.fishtank:css-selectors:1.0.5',
                             'org.testinfected.hamcrest-matchers:dom-matchers:1.8@jar'],
        htmlunit          : 'net.sourceforge.htmlunit:htmlunit:2.14',
        molecule_test     : 'com.vtence.molecule:molecule:0.10:tests',
        selenium_api      : 'org.seleniumhq.selenium:selenium-api:2.44.0',
        selenium_remote   : 'org.seleniumhq.selenium:selenium-remote-driver:2.44.0',
        selenium_firefox  : 'org.seleniumhq.selenium:selenium-firefox-driver:2.44.0',
        selenium_phantom  : 'com.github.detro:phantomjsdriver:1.2.0',
        windowlicker_core : 'com.googlecode.windowlicker:windowlicker-core:r268@jar',
        windowlicker_web  : 'com.googlecode.windowlicker:windowlicker-web:r268@jar',
]

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile libs.gson
    compile libs.molecule
    compile libs.simple
    compile libs.jmustache

    testCompile libs.junit
    testCompile libs.jmock
    testCompile libs.hamcrest
    testCompile libs.htmlunit
    testCompile libs.molecule_test
    testCompile libs.hamcrest_dom
    testCompile libs.selenium_api
    testCompile libs.selenium_firefox
    testCompile(libs.selenium_phantom) {
        exclude module: 'selenium-java'
        exclude module: 'selenium-server'
    }
    testCompile libs.windowlicker_core
    testCompile libs.windowlicker_web
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task coverage(dependsOn: ['test', 'jacocoTestReport'])

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

ext {
    karmaDependencies = ['karma', 'karma-mocha', 'karma-chai', 'karma-effroi', 'karma-coverage',
                         'karma-phantomjs-launcher', 'karma-firefox-launcher', 'karma-chrome-launcher']
    karmaExec = file('node_modules/karma/bin/karma')
    karmaConfig = 'src/test/resources/karma.conf.js'
}

task karmaSetup(type: NpmTask) {
    outputs.dir file('node_modules')
    args = ['install', '--loglevel', 'error'] + karmaDependencies
}

task karmaRun(type: NodeTask, dependsOn: 'karmaSetup') {
    script = karmaExec
    args = ['start', karmaConfig, '--single-run']
}

task karmaWatch(type: NodeTask, dependsOn: 'karmaSetup') {
    script = karmaExec
    args = ['start', karmaConfig]
}

test.dependsOn karmaRun

mainClassName = 'com.vtence.jyose.JYose'
applicationName = 'jyose'

if (!project.hasProperty('port')) {
    ext.port = 8080
}

run {
    args port
    args file('src/main/webapp')
}

task stage(dependsOn: ['clean', 'installApp'])
